buildscript {
    repositories {
        mavenLocal()
        maven { url "https://nexus.ala.org.au/content/groups/public/" }
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:2.14.2"
        classpath "org.grails.plugins:hibernate5:${gormVersion-".RELEASE"}"
        classpath "gradle.plugin.com.energizedwork.webdriver-binaries:webdriver-binaries-gradle-plugin:1.4"
    }
}

plugins {
    id "com.gorylenko.gradle-git-properties" version "1.4.17"
}

ext {
    // The drivers we want to use
    drivers = ["firefox", "chrome", "chromeHeadless"]

    ext {
        gebVersion = '2.3'
        seleniumVersion = '3.14.0'
        chromeDriverVersion = '2.44'
        geckoDriverVersion = '0.26.0'
    }
}

version "4.0.6-SNAPSHOT"

group "au.org.ala"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"war"
apply plugin:"org.grails.grails-web"
apply plugin:"org.grails.grails-gsp"
apply plugin:"asset-pipeline"
apply plugin:"com.energizedwork.webdriver-binaries"

apply from: 'https://raw.githubusercontent.com/AtlasOfLivingAustralia/travis-build-configuration/master/travis_grails_publish.gradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://nexus.ala.org.au/content/groups/public/" }
    maven { url "https://repo.grails.org/grails/core" }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 1, 'minutes'
}

bootRepackage {
    enabled = false
}

// Inplace plugin config

// Set to true it needed only for development
boolean inplace = false

if (inplace) {
    grails {
        exploded = true
        plugins {
            // When not inplace used, the dependencies below are managed by biocache-hubs plugin
            compile project(':biocache-hubs')
            compile project(':downloads-plugin')
//            compile project(':ala-charts-plugin')
//            compile project(':images-client-plugin')
//            compile project(':ala-bootstrap3')
//            runtime project(':ala-ws-plugin')
        }
    }
}

dependencies {

    def noCache = {
        exclude group: 'org.grails.plugins', module: 'cache'
        exclude group: 'org.ehcache', module: 'ehcache'
    }

    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    provided "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-dependencies", noCache
    compile "org.grails:grails-web-boot"
    compile "org.grails.plugins:cache"
    compile "org.grails.plugins:scaffolding"
    compile "org.grails.plugins:hibernate5"
    compile "org.hibernate:hibernate-core:5.1.3.Final"
    compile 'org.grails.plugins:external-config:1.1.1'
    console "org.grails:grails-console"
    profile "org.grails.profiles:web"
    runtime "com.bertramlabs.plugins:asset-pipeline-grails:2.14.2"
    runtime "com.h2database:h2"
    testCompile "org.grails:grails-plugin-testing"
    testCompile "org.grails.plugins:geb"
    testCompile "org.grails:grails-test-mixins:3.3.0"
//    testRuntime "org.seleniumhq.selenium:selenium-htmlunit-driver:2.47.1"
//    testRuntime "net.sourceforge.htmlunit:htmlunit:2.18"

    // Jars
    compile "org.apache.httpcomponents:httpclient:4.5.6"
    compile 'xml-apis:xml-apis:1.4.01' // really?

    // ALA Plugins
    compile "org.grails.plugins:ala-auth:3.1.3", noCache
    compile "org.grails.plugins:ala-bootstrap3:3.2.3", noCache
    compile "org.grails.plugins:ala-admin-plugin:2.2", noCache

    if (!inplace) {
        compile "au.org.ala.plugins.grails:biocache-hubs:3.0.6-SNAPSHOT", noCache
        compile "au.org.ala.plugins.grails:downloads-plugin:2.2.0", noCache
    }

    // If using Spock, need to depend on geb-spock
    testCompile "org.gebish:geb-spock:$gebVersion"
    testCompile("org.spockframework:spock-core:1.3-groovy-2.4") {
        exclude group: "org.codehaus.groovy"
    }

    // If using JUnit, need to depend on geb-junit (3 or 4)
    testCompile "org.gebish:geb-junit4:$gebVersion"

    // Drivers
    testCompile "org.seleniumhq.selenium:selenium-chrome-driver:$seleniumVersion"
    testCompile "org.seleniumhq.selenium:selenium-firefox-driver:$seleniumVersion"

    testCompile "org.seleniumhq.selenium:selenium-remote-driver:$seleniumVersion"
    testCompile "org.seleniumhq.selenium:selenium-api:$seleniumVersion"
    testCompile "org.seleniumhq.selenium:selenium-support:$seleniumVersion"
}

webdriverBinaries {
    chromedriver "$chromeDriverVersion"
    geckodriver "$geckoDriverVersion"
}


tasks.getByName("integrationTest") {
    group JavaBasePlugin.VERIFICATION_GROUP

    //outputs.upToDateWhen { false }  // Always run tests

    systemProperty "geb.build.reportsDir", reporting.file("geb/$name")

    systemProperty "geb.env",  System.properties['driver']?:"firefox"  // firefox by default
    // pass test server by -DbaseUrl
    systemProperty "geb.build.baseUrl", System.properties['baseUrl'] ?: "https://biocache-test.ala.org.au/"
    //Store username and password into the default config file
    systemProperty "configFile",System.properties['configFile']?: "/data/ala-hub/test/default.properties"
    //or pass with -Dusername, -Dpassword
    systemProperty "username", System.properties['username']
    systemProperty "password", System.properties['password']
}


bootRun {
    jvmArgs('-Dspring.output.ansi.enabled=always')
    addResources = true
}


assets {
    minifyJs = true
    minifyCss = true
}
